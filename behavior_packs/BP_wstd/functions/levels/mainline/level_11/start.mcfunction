# ===== 启动&重启关卡 =====

# --- 基本 ---

## ID  | 通用函数要求：需将本命令提前于通用函数执行
scoreboard players set level data 11

## 通用函数
function lib/modify_data/level/start

## 状态（时间线&剧情线） | 通用函数要求：需将本命令延后于通用函数执行
function lib/modify_states/timeline/enable_6

## 重置关卡房间
execute if score language settings matches 0 run structure load mainline:level_11 51 20 55
execute if score language settings matches 1 run structure load mainline:level_11_en 51 20 55

## 给予玩家辅助功能物品
execute as @a run function lib/modify_data/item/aux_functions

# --- 玩家 ---

## 传送玩家
tp @a 59 22 63 facing 52 22 56

## 标题&副标题 | 通用函数要求：需将本命令延后于通用函数执行
titleraw @a title {"rawtext":[{"translate":"title.level_11"}]}
titleraw @a subtitle {"rawtext":[{"translate":"subtitle.level_11"}]}
